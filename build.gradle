plugins {
    id 'com.zoltu.kotlin' version '1.0.2'
}

apply plugin: 'java'
apply plugin: 'application'
apply plugin: com.easternedgerobotics.gradle.MultipleStartScripts
apply plugin: 'jacoco'
apply plugin: 'checkstyle'
apply plugin: 'idea'

applicationDefaultJvmArgs = [
    '-Xloggc:gc.log', '-XX:+PrintGCApplicationStoppedTime', '-XX:+PrintGCTimeStamps'
]
applicationName = 'topside'
version = '9.0.0'
mainClassName = 'com.easternedgerobotics.rov.Topside'
sourceCompatibility = JavaVersion.VERSION_1_8

checkstyle {
    toolVersion = '6.18'
}

jacoco {
    toolVersion = '0.7.5.201505241946'
}

repositories {
    jcenter()
}

dependencies {
    compile 'com.fasterxml.jackson.core:jackson-databind:2.5.3'
    compile 'com.neuronrobotics:nrjavaserial:3.11.0'
    compile 'com.pi4j:pi4j-core:1.0'
    compile 'commons-cli:commons-cli:1.3.1'
    compile 'io.reactivex:rxjava:1.0.12'
    compile 'io.reactivex:rxjavafx:0.0.5'
    compile 'javax.inject:javax.inject:1'
    compile 'net.java.jinput:jinput:2.0.6'
    compile 'org.jetbrains.kotlin:kotlin-stdlib:1.0.2'
    compile 'org.tinylog:tinylog:1.0.3'
    compile 'rxbroadcast:rxbroadcast:0.0.4'
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.0.53-beta'
}

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << '-Xlint:unchecked' << '-Xdiags:verbose'
        }
    }
}

test {
    testLogging {
        exceptionFormat = 'full'
        showStandardStreams = true
        events 'passed', 'skipped', 'failed'
    }
}

multipleStartScripts {
    mainClasses = [
        'picamera-a': 'com.easternedgerobotics.rov.PicameraA',
        'picamera-b': 'com.easternedgerobotics.rov.PicameraB',
        'rov': 'com.easternedgerobotics.rov.Rov',
        'topside': 'com.easternedgerobotics.rov.Topside',
        'reset-gui': 'com.easternedgerobotics.rov.ResetGUI',
    ]
}

task testJar(type: Jar) {
    from sourceSets.test.output + sourceSets.main.output
    from(configurations.testCompile.collect { it.isDirectory() ? it : zipTree(it) }) {
        exclude 'META-INF/**'
    }

    classifier 'tests'
    description 'Assembles a jar archive containing the test classes.'
    group 'verification'
}
